#version 330 core

const int NUM_LIGHTS = 9;

// Interpolated values from the vertex shader
in vec2 UV;
in vec3 Position_worldspace;
in vec3 Normal_cameraspace;
in vec3 EyeDirection_cameraspace;
in vec3 LightDirection_cameraspace[NUM_LIGHTS];
// in vec4 ShadowCoord;

// Output data
out vec3 color;

// Values that stay constant for the whole mesh.
uniform sampler2D myTextureSampler;
uniform bool useTexture;
uniform vec3 materialColor;
uniform mat4 MV;
uniform vec3 LightPosition_worldspace[NUM_LIGHTS];


void main() {

    vec3 LightColor = vec3(1,1,1);
    float LightPower = 2.0f;

    vec3 baseColor = useTexture ? texture(myTextureSampler, UV).rgb : materialColor;
    vec3 MaterialDiffuseColor  = baseColor;
    vec3 MaterialAmbientColor  = 0.1 * MaterialDiffuseColor;
    vec3 MaterialSpecularColor = vec3(0.3);

    vec3 n = normalize(Normal_cameraspace);
    vec3 E = normalize(EyeDirection_cameraspace);

    vec3 colorAccum = vec3(0.0);

    for (int i = 0; i < NUM_LIGHTS; i++) {
        float distance = length(LightPosition_worldspace[i] - Position_worldspace);

        vec3 l = normalize(LightDirection_cameraspace[i]);
        float cosTheta = clamp(dot(n, l), 0.0, 1.0);
        vec3 R = reflect(-l, n);
        float cosAlpha = clamp(dot(E, R), 0.0, 1.0);

        float attenuation = LightPower / (distance * distance);
		colorAccum +=
            MaterialDiffuseColor * LightColor * attenuation * cosTheta +
            MaterialSpecularColor * LightColor * attenuation * pow(cosAlpha, 5.0);
    }

    color = MaterialAmbientColor + colorAccum;
}
