#version 330 core
layout(location = 0) in vec3 vertexPosition_modelspace;
layout(location = 1) in vec2 vertexUV;
layout(location = 2) in vec3 vertexNormal_modelspace;

const int NUM_LIGHTS = 3;

out vec2 UV;
out vec3 vertexColor;

uniform mat4 MVP;
uniform mat4 V;
uniform mat4 M;
uniform vec3 LightPosition_worldspace[NUM_LIGHTS];
uniform bool useTexture;
uniform sampler2D myTextureSampler;
uniform vec3 materialColor;

void main() {
    gl_Position = MVP * vec4(vertexPosition_modelspace, 1.0);
    UV = vertexUV;

    // camera space
    vec3 vertexPosition_cameraspace = (V * M * vec4(vertexPosition_modelspace,1)).xyz;
    vec3 EyeDirection_cameraspace = -vertexPosition_cameraspace;

    vec3 Normal_cameraspace = (V * M * vec4(vertexNormal_modelspace, 0)).xyz;

    vec3 LightColor = vec3(1.0);
    float LightPower = 1.0f;

    vec3 baseColor = useTexture ? texture(myTextureSampler, UV).rgb : materialColor;
    vec3 diffColor = baseColor;
    vec3 ambColor = 0.1 * diffColor;
    vec3 specColor = vec3(0.3);

    vec3 colorAccum = vec3(0.0);
    
    vec3 E = normalize(EyeDirection_cameraspace);

    for(int i=0; i<NUM_LIGHTS; i++){
        vec3 LightPosition_cameraspace = (V * vec4(LightPosition_worldspace[i],1)).xyz;
        vec3 LightDirection_cameraspace = LightPosition_cameraspace + EyeDirection_cameraspace;
        vec3 n = normalize(Normal_cameraspace);
        vec3 l = normalize(LightDirection_cameraspace);
        vec3 R = reflect(-l, n);

        float cosTheta = max(dot(n, l), 0.0);
        float cosAlpha = max(dot(E, R), 0.0);

        float distance = length(LightPosition_worldspace[i] - (M * vec4(vertexPosition_modelspace,1)).xyz);
        colorAccum += diffColor * LightColor * LightPower * cosTheta / (distance*distance) +
                    specColor * LightColor * LightPower * pow(cosAlpha, 5.0) / (distance*distance);
    }
    vertexColor = ambColor + colorAccum;    
}
